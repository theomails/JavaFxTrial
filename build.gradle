plugins {
    id 'application'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    modularity.inferModulePath = true
}

group = 'com.vrrm'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:33.0.0-jre"
    implementation "com.miglayout:miglayout-javafx:11.2"

    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"

    testCompileOnly "org.projectlombok:lombok:1.18.32"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.32"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.graphics' ]
}

test {
    useJUnitPlatform()
}

application {
    mainModule = 'com.vrrm.javafxtrial'
    mainClass = 'com.vrrm.Main'
}

jlink {
    imageName = 'JavaFxTrial'

    mergedModule {
        requires 'java.logging';
    }

    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    ]

    launcher {
        name = 'JavaFxTrial'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '--add-reads', 'com.vrrm.javafxtrial=ALL-UNNAMED'  // Only needed for EventBus if it's non-modular
    ]
}

tasks.withType(JavaExec) {
    jvmArgs += [
            '--add-reads', 'com.vrrm.javafxtrial=ALL-UNNAMED'
    ]
}
